#!/usr/bin/perl
#
#  The plugin plugins/05-logger.js will keep a rotating
# buffer of the N most-recent submissions.
#
#  This utility will dump the given field from that
# buffer of JSON objects.
#
# Steve
# --


use strict;
use warnings;

use Redis;
use JSON;


#
#  Get the field name to dump, if specified.
#
my $field = shift;
$field = lc($field) if ($field);


#
#  The known fields
#
my %known;


#
#  Get the most recent logged entries
#
my $redis = new Redis();
my @recent = $redis->lrange( "recent-comments", 0, -1 );

#
#  Process each saved JSON object.
#
foreach my $ent (@recent)
{
    my $obj;

    # decode if we can
    eval {$obj = decode_json($ent)};
    next if ($@);

    # if we're showing a field ..
    if ($field)
    {

        # and that field exists.
        if ( $obj->{ $field } )
        {

            # show it
            print $obj->{ $field } . "\n";
        }
    }
    else
    {

        # record the fields we found in this object.
        foreach my $key ( keys %$obj )
        {
            $known{ $key } += 1;
        }
    }
}

#
#  If we didn't get a field show known-fields.
#
if ( !$field )
{
    foreach my $key ( keys %known )
    {
        print $key . "\n";
    }
}

$redis->quit();
exit(0);


